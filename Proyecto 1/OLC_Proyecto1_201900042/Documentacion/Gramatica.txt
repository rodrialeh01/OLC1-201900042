-------------------------------------------------------------   GRAMATICA  ---------------------------------------------------------------

G = (N,T,P,S)
    Donde:
        N = { NO TERMINALES }
        T = { TERMINALES }
        P = { PRODUCCIONES }
        S = { INICIO }

---------------------------NO TERMINALES-----------------------------------

NO TERMINALES = {
    INICIO,INSTRUCCIONES, DECLARACION,LISTA_IDENTIFICADORES, ASIGNACION, EXPRESION, TIPODATO, FUNCION, METODO, GLOBAL, LISTA_PARAMETROS, ENTORNO_LOCAL,
    CONDICIONAL_SI, CONDICION, LOCAL, LISTA_OSI, SELECCION_MULTIPLE, LISTA_VALORENTONCES, CICLO_PARA, CICLO_MIENTRAS, CICLO_REPETIR, EJECUTAR, IMPRESION;
    IMPRESION_CONSALTO, ALGORITMO, MAIN, COMENTARIOS, COMENTARIO, INSERCION_PARAMETROS, EJECUTAR_2, RELACIONAL, FYM, AGRUPACION
}

--------------------------- TERMINALES ------------------------------------

TERMINALES = {
    RINICIO,RFIN,RMETODO,RFINMETODO,RFUNCION,RFINFUNCION,RCONPARAMETROS,RRETORNAR,RINGRESAR,RCOMO,RCONVALOR,RSI,RENTONCES,ROSI,RDELOCONTRARIO,RFINSI,RSEGUN,
    RHACER,RFINSEGUN,COMENTARIOL,COMENTARIOML,RPARA,RHASTA,RCONINCREMENTAL,RFINPARA,RMIENTRAS,RFINMIENTRAS,RREPETIR,RHASTAQUE,REJECUTAR,RIMPRIMIR,RIMPRIMIRNL,
    IDENTIFICADOR,ENTERO,CARASCCI,CADENA,CARACTER,DECIMAL,SUMA,RESTA,MULTIPLICACION,DIVIDIR,MODU,POTENCIA,RMAYOR,RMENOR,RMAYOROIGUAL,RMENOROIGUAL,RESIGUAL,
    RESDIFERENTE,ROR,RAND,RNOT,RNUMERO,RCADENA,RBOOLEAN,RCARACTER,RVERDADERO,RFALSO,PARIZQ,PARDER,INTABRE,INTCIERRA,FLECHA,PTCOMA,COMA,CORIZQ,CORDER
}

---------------------------PRODUCCIONES------------------------------------
INICIO::=INSTRUCCIONES                                                                                                                                                             

INSTRUCCIONES::=MAIN                                                                                  
               |COMENTARIOS                                                                                     

MAIN::=RINICIO ALGORITMO RFIN                                                                                                                                 
      |RINICIO RFIN                                                                                     
      |error RFIN

COMENTARIOS::=COMENTARIOS COMENTARIO
             |COMENTARIO

COMENTARIO::=COMENTARIOL
            |COMENTARIOML

ALGORITMO::=ALGORITMO GLOBAL                                                                                    
           |GLOBAL

GLOBAL::= ENTORNO_LOCAL
         |FYM

FYM::=FUNCION
     |METODO

DECLARACION::=RINGRESAR LISTA_IDENTIFICADORES RCOMO TIPODATO RCONVALOR CONDICION PTCOMA

LISTA_IDENTIFICADORES::=LISTA_IDENTIFICADORES COMA IDENTIFICADOR
                       |IDENTIFICADOR

EXPRESION::=ENTERO
            |DECIMAL
            |CADENA
            |CARACTER
            |CARASCCI
            |RVERDADERO
            |RFALSO
            |IDENTIFICADOR
            |RESTA EXPRESION
            |EXPRESION SUMA EXPRESION
            |EXPRESION RESTA EXPRESION 
            |EXPRESION MULTIPLICACION EXPRESION
            |EXPRESION DIVIDIR EXPRESION        
            |EXPRESION POTENCIA CORIZQ EXPRESION CORDER 
            |EXPRESION MODU EXPRESION
            |AGRUPACION 
            |EJECUTAR_2

RELACIONAL::=EXPRESION
           |EXPRESION RMAYOR EXPRESION
           |EXPRESION RMENOR EXPRESION
           |EXPRESION RMAYOROIGUAL EXPRESION
           |EXPRESION RMENOROIGUAL EXPRESION
           |EXPRESION RESIGUAL EXPRESION
           |EXPRESION RESDIFERENTE EXPRESION

CONDICION::=RELACIONAL ROR RELACIONAL
           |RELACIONAL RAND RELACIONAL
           |RNOT RELACIONAL
           |RELACIONAL

AGRUPACION::=PARIZQ CONDICION PARDER

ASIGNACION::=LISTA_IDENTIFICADORES FLECHA EXPRESION PTCOMA

FUNCION::=RFUNCION IDENTIFICADOR TIPODATO LOCAL RRETORNAR CONDICION PTCOMA RFINFUNCION
         |RFUNCION IDENTIFICADOR TIPODATO RRETORNAR CONDICION PTCOMA RFINFUNCION
         |RFUNCION IDENTIFICADOR TIPODATO RCONPARAMETROS PARIZQ LISTA_PARAMETROS PARDER LOCAL RRETORNAR CONDICION PTCOMA RFINFUNCION
         |RFUNCION IDENTIFICADOR TIPODATO RCONPARAMETROS PARIZQ LISTA_PARAMETROS PARDER RRETORNAR CONDICION PTCOMA RFINFUNCION

METODO::=RMETODO IDENTIFICADOR LOCAL RFINMETODO
        |RMETODO IDENTIFICADOR RFINMETODO
        |RMETODO IDENTIFICADOR RCONPARAMETROS PARIZQ LISTA_PARAMETROS PARDER LOCAL RFINMETODO
        |RMETODO IDENTIFICADOR RCONPARAMETROS PARIZQ LISTA_PARAMETROS PARDER RFINMETODO

LISTA_PARAMETROS::=LISTA_PARAMETROS COMA IDENTIFICADOR TIPODATO
                  |IDENTIFICADOR TIPODATO                               

TIPODATO::=RNUMERO                                                                      
          |RCADENA                                                                        
          |RBOOLEAN                                                                         
          |RCARACTER                                                              

LOCAL::=LOCAL ENTORNO_LOCAL 
       |ENTORNO_LOCAL

ENTORNO_LOCAL::=ASIGNACION
               |DECLARACION
               |CONDICIONAL_SI
               |SELECCION_MULTIPLE
               |CICLO_PARA
               |CICLO_MIENTRAS
               |CICLO_REPETIR
               |EJECUTAR
               |IMPRESION
               |IMPRESION_CONSALTO
               |COMENTARIOL
               |COMENTARIOML

CONDICIONAL_SI::=RSI CONDICION RENTONCES LOCAL RFINSI                                
                |RSI CONDICION RENTONCES LOCAL LISTA_OSI RFINSI   
                |RSI CONDICION RENTONCES LOCAL LISTA_OSI RDELOCONTRARIO LOCAL RFINSI 
                |RSI CONDICION RENTONCES LOCAL RDELOCONTRARIO LOCAL RFINSI 

LISTA_OSI::=LISTA_OSI ROSI CONDICION RENTONCES LOCAL
            |ROSI CONDICION RENTONCES LOCAL 

SELECCION_MULTIPLE::=RSEGUN EXPRESION RHACER LISTA_VALORENTONCES RFINSEGUN
                    |RSEGUN EXPRESION RHACER LISTA_VALORENTONCES RDELOCONTRARIO RENTONCES LOCAL RFINSEGUN

LISTA_VALORENTONCES::=LISTA_VALORENTONCES INTABRE EXPRESION INTCIERRA RENTONCES LOCAL
                     |INTABRE EXPRESION INTCIERRA RENTONCES LOCAL

CICLO_PARA::=RPARA IDENTIFICADOR FLECHA EXPRESION RHASTA EXPRESION RHACER LOCAL RFINPARA 
            |RPARA IDENTIFICADOR FLECHA EXPRESION RHASTA EXPRESION RHACER RFINPARA
            |RPARA IDENTIFICADOR FLECHA EXPRESION RHASTA EXPRESION RCONINCREMENTAL EXPRESION RHACER LOCAL RFINPARA
            |RPARA IDENTIFICADOR FLECHA EXPRESION RHASTA EXPRESION RCONINCREMENTAL EXPRESION RHACER RFINPARA 

CICLO_MIENTRAS::=RMIENTRAS CONDICION RHACER LOCAL RFINMIENTRAS 
                |RMIENTRAS CONDICION RHACER RFINMIENTRAS

CICLO_REPETIR::=RREPETIR LOCAL RHASTAQUE CONDICION 
               |RREPETIR RHASTAQUE CONDICION

EJECUTAR::=REJECUTAR IDENTIFICADOR PARIZQ PARDER PTCOMA
           |REJECUTAR IDENTIFICADOR PARIZQ INSERCION_PARAMETROS PARDER PTCOMA
           
EJECUTAR_2::=REJECUTAR IDENTIFICADOR PARIZQ PARDER
            |REJECUTAR IDENTIFICADOR PARIZQ INSERCION_PARAMETROS PARDER   

INSERCION_PARAMETROS::=INSERCION_PARAMETROS COMA EXPRESION 
                      |EXPRESION

IMPRESION::=RIMPRIMIR EXPRESION PTCOMA

IMPRESION_CONSALTO::= RIMPRIMIRNL EXPRESION PTCOMA