package analizadores; 
import Clases.ErrorLenguaje;
import java_cup.runtime.*;
import Structures.Instrucciones.Comentarios;
import Structures.Instrucciones.Declaracion;
import Structures.Instrucciones.Instruccion;
import Structures.Instrucciones.Operacion;
import Structures.Instrucciones.Si;
import Structures.Instrucciones.Main;
import Structures.Instrucciones.Asignacion;
import java.util.LinkedList;
import Structures.Nodo;
import Structures.Arbol;
import Structures.Produccion;


parser code 
{:
    /**
    *Listas
    **/
    public LinkedList<ErrorLenguaje> ErroresSintacticos = new LinkedList<ErrorLenguaje>();
    public LinkedList<Instruccion> Golang = new LinkedList<Instruccion>();
    /**
     * Método al que se llama automáticamente ante algún error sintactico.
     **/ 
    public void syntax_error(Symbol s){
        
        ErroresSintacticos.add(new ErrorLenguaje("Sintáctico","Carácter no esperado: "+s.value.toString(),s.right,s.left));

        System.out.println("Error Sintáctico en la Línea " + (s.left) +
        " Columna "+s.right+ ". No se esperaba este componente: " +s.value+"."); 
    } 

    /**
     * Método al que se llama automáticamente ante algún error sintáctico 
     * en el que ya no es posible una recuperación de errores.
     **/ 
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        ErroresSintacticos.add(new ErrorLenguaje("Sintáctico","Carácter no esperado: "+s.value.toString(),s.right,s.left));
        System.out.println("Error síntactico irrecuperable en la Línea " + 
        (s.left)+ " Columna "+s.right+". Componente " + s.value + 
        " no reconocido."); 
    }
:} 

terminal String RINICIO,RFIN,RMETODO,RFINMETODO,RFUNCION,RFINFUNCION,RCONPARAMETROS,RRETORNAR,RINGRESAR,RCOMO,RCONVALOR,RSI,RENTONCES,ROSI,RDELOCONTRARIO,RFINSI,RSEGUN,RHACER,RFINSEGUN;
terminal String COMENTARIOL,COMENTARIOML,RPARA,RHASTA,RCONINCREMENTAL,RFINPARA,RMIENTRAS,RFINMIENTRAS,RREPETIR,RHASTAQUE,REJECUTAR,RIMPRIMIR,RIMPRIMIRNL;
terminal String IDENTIFICADOR;
terminal String ENTERO;
terminal String CARASCCI;
terminal String CADENA;
terminal String CARACTER;
terminal String DECIMAL;
terminal String SUMA,RESTA,MULTIPLICACION,DIVIDIR,MODU,POTENCIA;
terminal String RMAYOR,RMENOR,RMAYOROIGUAL,RMENOROIGUAL,RESIGUAL,RESDIFERENTE,ROR,RAND,RNOT;
terminal String RNUMERO,RCADENA,RBOOLEAN,RCARACTER;
terminal String RVERDADERO,RFALSO;
terminal String PARIZQ,PARDER,INTABRE,INTCIERRA,FLECHA,PTCOMA,COMA,CORIZQ,CORDER;

non terminal INICIO;
non terminal INSTRUCCIONES;
non terminal DECLARACION;
non terminal LinkedList<Instruccion> LISTA_IDENTIFICADORES;
non terminal ASIGNACION;
non terminal EXPRESION;
non terminal TIPODATO;
non terminal GLOBAL;
non terminal FUNCION;
non terminal METODO;
non terminal LISTA_PARAMETROS;
non terminal ENTORNO_LOCAL;
non terminal CONDICIONAL_SI;
non terminal CONDICION;
non terminal LOCAL;
non terminal LISTA_OSI;
non terminal SELECCION_MULTIPLE;
non terminal LISTA_VALORENTONCES;
non terminal CICLO_PARA;
non terminal CICLO_MIENTRAS;
non terminal CICLO_REPETIR;
non terminal EJECUTAR;
non terminal IMPRESION;
non terminal IMPRESION_CONSALTO;
non terminal ALGORITMO;
non terminal MAIN;
non terminal COMENTARIOS;
non terminal COMENTARIO;
non terminal INSERCION_PARAMETROS;
non terminal EJECUTAR_2;
non terminal RELACIONAL;

precedence left SUMA,RESTA;
precedence left MULTIPLICACION,DIVIDIR;
precedence left POTENCIA,MODU;

//-------------------------------------------GRAMATICA SINTACTICA-------------------------------------------------
start with INICIO; 

INICIO::=INSTRUCCIONES                                                                                                                                                             
;
INSTRUCCIONES::=MAIN                                                                                  
               |COMENTARIOS                                                                           
               |error
;
MAIN::=RINICIO ALGORITMO RFIN                                                                         
      |RINICIO RFIN
;
COMENTARIOS::=COMENTARIOS COMENTARIO
             |COMENTARIO
;
COMENTARIO::=COMENTARIOL:a                                                                              {:  
                                                                                                            RESULT=new Comentarios(Comentarios.Tipo_Comentario.COMENTARIO_UNILINEA, a);
                                                                                                            Golang.add((Comentarios) RESULT);
                                                                                                        :}
            |COMENTARIOML:a                                                                             {:  
                                                                                                            RESULT=new Comentarios(Comentarios.Tipo_Comentario.COMENTARIO_MILTILINEA, a);
                                                                                                            Golang.add((Comentarios) RESULT);
                                                                                                        :}
;
ALGORITMO::=ALGORITMO GLOBAL
           |GLOBAL
;
GLOBAL::=FUNCION
        |METODO
        |ENTORNO_LOCAL
;
DECLARACION::=RINGRESAR LISTA_IDENTIFICADORES:l RCOMO RNUMERO RCONVALOR EXPRESION:e PTCOMA              {:  
                                                                                                            RESULT=new Declaracion(Declaracion.Tipo_Variable.RNUMERO,(Operacion)e,(LinkedList)l);
                                                                                                            Golang.add((Declaracion) RESULT);
                                                                                                        :}                                    
             |RINGRESAR LISTA_IDENTIFICADORES:l RCOMO RCADENA RCONVALOR EXPRESION:e PTCOMA              {:  
                                                                                                            RESULT=new Declaracion(Declaracion.Tipo_Variable.RCADENA,(Operacion)e,(LinkedList)l);
                                                                                                            Golang.add((Declaracion) RESULT);
                                                                                                        :}  
             |RINGRESAR LISTA_IDENTIFICADORES:l RCOMO RBOOLEAN RCONVALOR EXPRESION:e PTCOMA             {:  
                                                                                                            RESULT=new Declaracion(Declaracion.Tipo_Variable.RBOOLEAN,(Operacion)e,(LinkedList)l);
                                                                                                            Golang.add((Declaracion) RESULT);
                                                                                                        :}  
             |RINGRESAR LISTA_IDENTIFICADORES:l RCOMO RCARACTER RCONVALOR EXPRESION:e PTCOMA            {:  
                                                                                                            RESULT=new Declaracion(Declaracion.Tipo_Variable.RCARACTER,(Operacion)e,(LinkedList)l);
                                                                                                            Golang.add((Declaracion) RESULT);
                                                                                                        :} 
;
LISTA_IDENTIFICADORES::=LISTA_IDENTIFICADORES:a COMA IDENTIFICADOR:b                                    {:  
                                                                                                            RESULT = new LinkedList<>();
                                                                                                            for (int i = 0; i < a.size(); i++) {
                                                                                                                RESULT.add(a.get(i));
                                                                                                            }
                                                                                                            RESULT.add(new Operacion(b,Operacion.Tipo_operacion.IDENTIFICADOR));
                                                                                                        :} 
                       |IDENTIFICADOR:a                                                                 {:  
                                                                                                            RESULT= new LinkedList<Instruccion>();
                                                                                                            RESULT.add(new Operacion(a,Operacion.Tipo_operacion.IDENTIFICADOR));
                                                                                                        :}
;
EXPRESION::=ENTERO:a                                                                                    {:  
                                                                                                            RESULT=new Operacion(a,Operacion.Tipo_operacion.NUMERO);
                                                                                                        :}
            |DECIMAL:a                                                                                  {:  
                                                                                                            RESULT=new Operacion(a,Operacion.Tipo_operacion.DECIMAL);
                                                                                                        :}
            |CADENA:a                                                                                   {:  
                                                                                                            RESULT=new Operacion(a,Operacion.Tipo_operacion.CADENA);
                                                                                                        :}
            |CARACTER:a                                                                                 {:  
                                                                                                            RESULT=new Operacion(a,Operacion.Tipo_operacion.CARACTER);
                                                                                                        :}
            |CARASCCI:a                                                                                 {:  
                                                                                                            RESULT=new Operacion(a,Operacion.Tipo_operacion.ASCCI);
                                                                                                        :}
            |RVERDADERO:a                                                                               {:  
                                                                                                            RESULT=new Operacion(a,Operacion.Tipo_operacion.VERDADERO);
                                                                                                        :}
            |RFALSO:a                                                                                   {:  
                                                                                                            RESULT=new Operacion(a,Operacion.Tipo_operacion.FALSO);
                                                                                                        :}
            |IDENTIFICADOR:a                                                                            {:  
                                                                                                            RESULT=new Operacion(a,Operacion.Tipo_operacion.IDENTIFICADOR);
                                                                                                        :}
            |RESTA EXPRESION:a                                                                          {:  
                                                                                                            RESULT=new Operacion((Operacion)a,Operacion.Tipo_operacion.NEGATIVO);
                                                                                                        :}
            |PARIZQ EXPRESION:a PARDER                                                                  {:  
                                                                                                            RESULT=new Operacion((Operacion)a,Operacion.Tipo_operacion.AGRUPACION);
                                                                                                        :} 
            |EXPRESION:a SUMA EXPRESION:b                                                               {:  
                                                                                                            RESULT=new Operacion((Operacion)a,(Operacion)b,Operacion.Tipo_operacion.SUMA);
                                                                                                        :} 
            |EXPRESION:a RESTA EXPRESION:b                                                              {:  
                                                                                                            RESULT=new Operacion((Operacion)a,(Operacion)b,Operacion.Tipo_operacion.RESTA);
                                                                                                        :} 
            |EXPRESION:a MULTIPLICACION EXPRESION:b                                                     {:  
                                                                                                            RESULT=new Operacion((Operacion)a,(Operacion)b,Operacion.Tipo_operacion.MULTIPLICACION);
                                                                                                        :} 
            |EXPRESION:a DIVIDIR EXPRESION:b                                                            {:  
                                                                                                            RESULT=new Operacion((Operacion)a,(Operacion)b,Operacion.Tipo_operacion.DIVISION);
                                                                                                        :}        
            |EXPRESION:a POTENCIA CORIZQ EXPRESION:b CORDER                                             {:  
                                                                                                            RESULT=new Operacion((Operacion)a,(Operacion)b,Operacion.Tipo_operacion.POTENCIA);
                                                                                                        :} 
            |EXPRESION:a MODU EXPRESION:b                                                               {:  
                                                                                                            RESULT=new Operacion((Operacion)a,(Operacion)b,Operacion.Tipo_operacion.MODULAR);
                                                                                                        :}                                                                   
;
ASIGNACION::=LISTA_IDENTIFICADORES:l FLECHA EXPRESION:e PTCOMA                                          {:  
                                                                                                            RESULT=new Asignacion((Operacion)e,(LinkedList)l);
                                                                                                            Golang.add((Asignacion) RESULT);
                                                                                                        :}
;
FUNCION::=RFUNCION IDENTIFICADOR TIPODATO LOCAL RRETORNAR CONDICION PTCOMA RFINFUNCION
         |RFUNCION IDENTIFICADOR TIPODATO RRETORNAR CONDICION PTCOMA RFINFUNCION
         |RFUNCION IDENTIFICADOR TIPODATO RCONPARAMETROS PARIZQ LISTA_PARAMETROS PARDER LOCAL RRETORNAR CONDICION PTCOMA RFINFUNCION
         |RFUNCION IDENTIFICADOR TIPODATO RCONPARAMETROS PARIZQ LISTA_PARAMETROS PARDER RRETORNAR CONDICION PTCOMA RFINFUNCION
;
METODO::=RMETODO IDENTIFICADOR LOCAL RFINMETODO
        |RMETODO IDENTIFICADOR RFINMETODO
        |RMETODO IDENTIFICADOR RCONPARAMETROS PARIZQ LISTA_PARAMETROS PARDER LOCAL RFINMETODO
        |RMETODO IDENTIFICADOR RCONPARAMETROS PARIZQ LISTA_PARAMETROS PARDER RFINMETODO
;
LISTA_PARAMETROS::=LISTA_PARAMETROS COMA IDENTIFICADOR TIPODATO
                  |IDENTIFICADOR TIPODATO
;
LOCAL::=LOCAL ENTORNO_LOCAL
       |ENTORNO_LOCAL 
;
ENTORNO_LOCAL::=ASIGNACION
               |DECLARACION
               |CONDICIONAL_SI
               |SELECCION_MULTIPLE
               |CICLO_PARA
               |CICLO_MIENTRAS
               |CICLO_REPETIR
               |EJECUTAR
               |IMPRESION
               |IMPRESION_CONSALTO
               |COMENTARIOL
               |COMENTARIOML
;
CONDICIONAL_SI::=RSI CONDICION:a RENTONCES LOCAL RFINSI 
                |RSI CONDICION LOCAL LISTA_OSI RFINSI 
                |RSI CONDICION LOCAL LISTA_OSI RDELOCONTRARIO LOCAL RFINSI 
                |RSI CONDICION:a LOCAL:b RDELOCONTRARIO LOCAL:c RFINSI  
;
RELACIONAL::=EXPRESION
           |EXPRESION RMAYOR EXPRESION
           |EXPRESION RMENOR EXPRESION
           |EXPRESION RMAYOROIGUAL EXPRESION
           |EXPRESION RMENOROIGUAL EXPRESION
           |EXPRESION RESIGUAL EXPRESION
           |EXPRESION RESDIFERENTE EXPRESION
;
CONDICION::=RELACIONAL ROR RELACIONAL
           |RELACIONAL RAND RELACIONAL
           |RNOT RELACIONAL
           |RELACIONAL                                                                          
;
LISTA_OSI::=LISTA_OSI ROSI CONDICION RENTONCES LOCAL
            |ROSI CONDICION RENTONCES LOCAL 
;
SELECCION_MULTIPLE::=RSEGUN EXPRESION RHACER LISTA_VALORENTONCES RFINSEGUN
                    |RSEGUN EXPRESION RHACER LISTA_VALORENTONCES RDELOCONTRARIO RENTONCES LOCAL RFINSEGUN
;
LISTA_VALORENTONCES::=LISTA_VALORENTONCES INTABRE EXPRESION INTCIERRA RENTONCES LOCAL
                     |INTABRE EXPRESION INTCIERRA RENTONCES LOCAL
;
CICLO_PARA::=RPARA IDENTIFICADOR FLECHA EXPRESION RHASTA EXPRESION RHACER LOCAL RFINPARA
            |RPARA IDENTIFICADOR FLECHA EXPRESION RHASTA EXPRESION RHACER RFINPARA
            |RPARA IDENTIFICADOR FLECHA EXPRESION RHASTA EXPRESION RCONINCREMENTAL EXPRESION RHACER LOCAL RFINPARA
            |RPARA IDENTIFICADOR FLECHA EXPRESION RHASTA EXPRESION RCONINCREMENTAL EXPRESION RHACER RFINPARA
;
CICLO_MIENTRAS::=RMIENTRAS CONDICION RHACER LOCAL RFINMIENTRAS
                |RMIENTRAS CONDICION RHACER RFINMIENTRAS
;
CICLO_REPETIR::=RREPETIR LOCAL RHASTAQUE CONDICION
               |RREPETIR RHASTAQUE CONDICION
;
EJECUTAR::=REJECUTAR IDENTIFICADOR PARIZQ PARDER PTCOMA
           |REJECUTAR IDENTIFICADOR PARIZQ INSERCION_PARAMETROS PARDER PTCOMA
;
EJECUTAR_2::=REJECUTAR IDENTIFICADOR PARIZQ PARDER
            |REJECUTAR IDENTIFICADOR PARIZQ INSERCION_PARAMETROS PARDER
;
INSERCION_PARAMETROS::=INSERCION_PARAMETROS COMA EXPRESION
                      |EXPRESION
;
IMPRESION::=RIMPRIMIR EXPRESION PTCOMA;
IMPRESION_CONSALTO::= RIMPRIMIRNL EXPRESION PTCOMA;